import groovy.sql.Sql

apply plugin: 'java'
apply plugin: 'groovy'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath group: 'org.xerial', name: 'sqlite-jdbc', version: '3.14.2.1'
    }
}

URLClassLoader loader = GroovyObject.class.classLoader
buildscript.configurations.classpath.each { File file ->
    loader.addURL(file.toURL())
}

ext {
    sql = Sql.newInstance('jdbc:sqlite:db-config.sqlite', 'org.sqlite.JDBC')
    tvShowDirectory = new File(project.property('tvShowDirectory'))
    completedDownloadsDirectory = new File(project.property('completedDownloadsDirectory'))
    rssFeed = project.property('rssFeed')

    if (project.hasProperty('completedDownloadsFileExclusions')) {
        completedDownloadsFileExclusions = project.property('completedDownloadsFileExclusions').toString().tokenize(',')

        logger.lifecycle "Using explicit completed downloads file exclusions: ${completedDownloadsFileExclusions}"
    } else {
        completedDownloadsFileExclusions = ['**/*.ass', '**/*.css', '**/*.divx', '**/*.DS_Store', '**/*.html', '**/*.jpg',
                                            '**/*.js', '**/*.nfo', '**/*.sfv', '**/*.srr', '**/*.srt', '**/*.txt', '**/*sample.*']

        logger.lifecycle "Using default completed downloads file exclusions: ${completedDownloadsFileExclusions}, override with -PcompletedDownloadsFileExclusions='<pattern1>, <pattern2>'"
    }

    flexgetDir = new File(project.buildDir, 'flexget')
}

task createVirtualEnv(type: Exec) {
    outputs.dir flexgetDir

    commandLine 'bash', '-c', "virtualenv \"${flexgetDir.absolutePath}\""
}

task installFlexget(type: Exec, dependsOn: [createVirtualEnv]) {
    inputs.dir flexgetDir
    outputs.file new File(flexgetDir, 'bin/flexget')

    workingDir flexgetDir
    commandLine './bin/pip', 'install', 'flexget'
}

task installTransmissionRPC(type: Exec, dependsOn: [createVirtualEnv]) {
    inputs.dir flexgetDir
    outputs.file new File(flexgetDir, 'bin/transmission-rpc')

    workingDir flexgetDir
    commandLine './bin/pip', 'install', 'transmission-rpc'
}

task flexget(type: Exec, dependsOn: [installFlexget, installTransmissionRPC]) {

    workingDir flexgetDir
    commandLine './bin/flexget', '-c', new File(project.projectDir, 'config.yml').absolutePath,
            '-l', "${project.buildDir}/flexget.log", 'execute', '--cli-config', "rssFeed=${rssFeed}"
}

task moveFinishedDownloads() {

    inputs.dir completedDownloadsDirectory
    outputs.upToDateWhen { true }

    doFirst {
        completedDownloadsDirectory.eachFile() { file ->

            def fileName = file.name
            logger.lifecycle "Working on: ${fileName}"

            def showFinder = (fileName =~ /^(?i)(.*)S\d{1,2}E\d{1,2}.*/)

            if (!showFinder.matches()) {
                return;
            }

            String show = showFinder.group(1).replaceAll(/\./, ' ').trim()
            logger.info "Determined show: $show"

            File showDir = new File(tvShowDirectory, show)
            showDir.mkdirs()

            if (file.isFile()) {
                logger.lifecycle "Moving ${fileName} to ${showDir}"

                file.renameTo(new File(showDir, file.getName()));
            } else {
                logger.lifecycle "Moving contents of ${fileName} to ${showDir}"

                ant.move(todir: showDir, includeEmptyDirs: false) {
                    fileset(dir: file, excludes: completedDownloadsFileExclusions.join(', ')) {
                        include(name: '**/**')
                    }
                    flattenmapper()
                }

                ant.delete(dir: file)
            }
        }
    }
}

task run(dependsOn: [flexget, moveFinishedDownloads])